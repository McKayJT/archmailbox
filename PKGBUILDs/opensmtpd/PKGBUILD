# Maintainer: adenosine <adenosine at octet dot space>
# Contibutor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd-libressl
_pkgname=opensmtpd
_extraver=6.4.0
pkgver=6.4.1p2
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('x86_64')
url='https://www.opensmtpd.org/'
license=('custom')
depends=('pam' 'libsodium' 'sqlite')
makedepends=('cmake' 'ninja')
provides=('smtp-server' 'smtp-forwarder' 'opensmtpd')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases' 'etc/smtpd/sqlite.conf')
options=('emptydirs')
source=("https://www.opensmtpd.org/archives/$_pkgname-$pkgver.tar.gz"
	"https://www.opensmtpd.org/archives/opensmtpd-extras-$_extraver.tar.gz"
	'https://www.opensmtpd.org/archives/libasr-1.0.2.tar.gz'
	'https://ftp.openbsd.org/pub/OpenBSD/LibreSSL/libressl-2.9.0.tar.gz'
	'https://github.com/libevent/libevent/releases/download/release-2.1.8-stable/libevent-2.1.8-stable.tar.gz'
        'opensmtpd.sysusers'
        'smtpd.service'
        'smtpd.socket'
        'argon2.patch')
sha256sums=('b69b84934bcc4893ca334504411cd47f86ea04ac5e971a5746d44c473ac5f732'
            'cb66986b7c0d0113210f4fb9ca1447b7f50ca088165e6ca61b9015ff0f2d6226'
            'a6f5d1c6306938156da3427525572b9b16c1e6be6c69845d390bb63f41a58b34'
            'eb5f298408b723f11a0ca0192c122ecb79b4855bfdf7eea183a6264296a13cf4'
            '965cc5a8bb46ce4199a47e9b2c9e1cae3b137e8356ffdad6d94d3b9069b71dc2'
            'b38b64f1457b7227d55585edc711cce525e089516d16b093e6c44387380e6b13'
            'abf5baeb2a87c60d668ad18ea41cc08cab7a4f76339dd6df05de15cdaadaf922'
            '32d46de5562d01de445d04c93bcc9f94bf103539b676e449c32e3603a3866cf8'
            '9d4c27433aa4c2e81a707471afe332ce3e7eb4d6d543d7a13f25ce840e781cb4')

prepare() {
  sed -ri 's,/etc/mail,/etc/smtpd,g' "$_pkgname-$pkgver/smtpd/smtpd.conf"
  sed -ri 's,/etc/mail,/etc/smtpd,g' \
    "opensmtpd-extras-$_extraver/extras/tables/table-sqlite/sqlite.conf"
  cd $_pkgname-$pkgver
  patch -p1 -i ../argon2.patch
  mkdir -p "$srcdir/libs"
  mkdir -p "$srcdir/libressl-2.9.0/build"
}

build() {
  #libasr
  cd "$srcdir/libasr-1.0.2"
  ./configure --prefix=/usr --disable-shared
  make DESTDIR="$srcdir/libs" install

  #static libevent with no ssl
  cd "$srcdir/libevent-2.1.8-stable"
  ./autogen.sh
  ./configure --disable-libevent-regress --prefix=/usr --disable-shared --disable-openssl
  make DESTDIR="$srcdir/libs" install

  #libressl 
  cd "$srcdir/libressl-2.9.0/build"
  cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBRESSL_APPS=OFF \
    -DLIBRESSL_TESTS=OFF \
    ..

  ninja 
  DESTDIR="$srcdir/libs" ninja install

  #opensmptd
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --with-mantype=doc \
    --with-path-mbox=/var/spool/mail \
    --with-path-empty=/var/empty \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-auth-pam \
    --with-libevent="$srcdir/libs/usr" \
    --with-libssl="$srcdir/libs/usr" \
    --with-libasr="$srcdir/libs/usr" \
    --with-libs='-lsodium -lcrypto' \

  make

  #opensmptd-extras
  cd "$srcdir/opensmtpd-extras-$_extraver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --with-mantype=doc \
    --libexecdir=/usr/lib/smtpd \
    --with-path-empty=/var/empty \
    --with-user-smtpd=smtpd \
    --with-libevent="$srcdir/libs/usr" \
    --with-libssl="$srcdir/libs/usr" \
    --with-table-sqlite

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/sendmail"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/mailq"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/newaliases"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/makemap"

  # install license, systemd unit files, sysusers
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/smtpd.service" "$pkgdir/usr/lib/systemd/system/smtpd.service"
  install -Dm644 "$srcdir/smtpd.socket" "$pkgdir/usr/lib/systemd/system/smtpd.socket"
  install -Dm644 "$srcdir/opensmtpd.sysusers" "$pkgdir/usr/lib/sysusers.d/opensmtpd.conf"

  # install an empty aliases file (used by the default config)
  install -Dm644 /dev/null "$pkgdir/etc/smtpd/aliases"

  cd "../opensmtpd-extras-$_extraver"

  make DESTDIR="$pkgdir/" install

  # install sqlite.conf
  install -Dm644 "$srcdir/opensmtpd-extras-$_extraver/extras/tables/table-sqlite/sqlite.conf" \
    "$pkgdir/etc/smtpd/sqlite.conf"
}
