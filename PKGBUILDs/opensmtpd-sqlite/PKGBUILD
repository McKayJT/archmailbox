# Maintainer: adenosine <adenosine at octet dot space>
# Contibutor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd-sqlite
pkgver=6.4.0
pkgrel=1
pkgdesc='sqlite table backend for opensmtpd'
arch=('x86_64')
url='https://www.opensmtpd.org/'
license=('custom')
depends=('pam' 'opensmtpd' 'sqlite')
makedepends=('cmake' 'ninja')
backup=('etc/smtpd/sqlite.conf')
options=('emptydirs')
source=("https://www.opensmtpd.org/archives/opensmtpd-extras-$pkgver.tar.gz"
	'https://ftp.openbsd.org/pub/OpenBSD/LibreSSL/libressl-2.9.0.tar.gz'
	'https://github.com/libevent/libevent/releases/download/release-2.1.8-stable/libevent-2.1.8-stable.tar.gz')
sha256sums=('cb66986b7c0d0113210f4fb9ca1447b7f50ca088165e6ca61b9015ff0f2d6226'
            'eb5f298408b723f11a0ca0192c122ecb79b4855bfdf7eea183a6264296a13cf4'
            '965cc5a8bb46ce4199a47e9b2c9e1cae3b137e8356ffdad6d94d3b9069b71dc2')

prepare() {
  sed -ri 's,/etc/mail,/etc/smtpd,g' "opensmtpd-extras-$pkgver/extras/tables/table-sqlite/sqlite.conf"
  mkdir -p "$srcdir/libs"
  mkdir -p "$srcdir/libressl-2.9.0/build"
}

build() {
  #static libevent with no ssl
  cd "$srcdir/libevent-2.1.8-stable"
  ./autogen.sh
  ./configure --disable-libevent-regress --prefix=/usr --disable-shared --disable-openssl
  make DESTDIR="$srcdir/libs" install

  #libressl 
  cd "$srcdir/libressl-2.9.0/build"
  cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBRESSL_APPS=OFF \
    -DLIBRESSL_TESTS=OFF \
    ..

  ninja 
  DESTDIR="$srcdir/libs" ninja install

  #opensmptd
  cd "$srcdir/opensmtpd-extras-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --with-mantype=doc \
    --libexecdir=/usr/lib/smtpd \
    --with-path-empty=/var/empty \
    --with-user-smtpd=smtpd \
    --with-libevent="$srcdir/libs/usr" \
    --with-libssl="$srcdir/libs/usr" \
    --with-table-sqlite

  make
}

package() {
  cd "opensmtpd-extras-$pkgver"

  make DESTDIR="$pkgdir/" install

  # install an empty aliases file (used by the default config)
  install -Dm644 "$srcdir/opensmtpd-extras-$pkgver/extras/tables/table-sqlite/sqlite.conf" \
    "$pkgdir/etc/smtpd/sqlite.conf"
}
